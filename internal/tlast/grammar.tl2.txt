TL2File := TL2Combinator* EOF;
TL2Combinator := TL2Annotation* (TL2TypeDeclaration | TL2FuncDeclaration) scl;

TL2FuncDeclaration := TL2TypeName CRC32 TL2Field* funEq ((alias? TL2TypeRef) | TL2TypeDefinition);
TL2TypeDeclaration := TL2TypeName CRC32? (lts TL2TypeTemplate (cm TL2TypeTemplate)* gts)? eq TL2TypeDefinition;
TL2TypeDefDeclaration := TL2TypeName (lts TL2TypeTemplate (cm TL2TypeTemplate)* gts)? alias TL2TypeRef;

TL2TypeDefinition := TL2Field* | TL2UnionType;

TL2UnionType := vb? TL2UnionConstructor (vb TL2UnionConstructor)*;
TL2UnionTypeFromFirst := vb TL2UnionConstructor (vb TL2UnionConstructor)*;
TL2UnionTypeFromSecond := TL2UnionConstructor (vb TL2UnionConstructor)+;

TL2UnionConstructor := name (TL2TypeRef | TL2Field*);

TL2Field := (((name | deprecatedName) qm?) | usc) cl TL2TypeRef;

TL2TypeRef := TL2TypeApplication | TL2BracketType;

TL2TypeApplication := TL2TypeName (lts TL2TypeArgument (cm TL2TypeArgument)* gts)?;
TL2BracketType := lsb TL2TypeArgument? rsb TL2TypeRef;

TL2TypeArgument := TL2TypeRef | number;
TL2TypeTemplate := name cl name;

TL2TypeName := (name dot)? name;
TL2Annotation := at name;

// tokens
CRC32 := #[a-f0-9]{8};
number := [0-9]+;
name := [a-zA-Z][a-zA-Z0-9_]*;
deprecatedName := _name;
alias := <=>;
funEq := =>;
dot := \.;
cm := ,;
eq := =;
cl := :;
scl := \;;
qm := \?;
vb := \|;
lsb := \[;
rsb := \];
lts := <;
gts := >;
at := @;
usc := _;