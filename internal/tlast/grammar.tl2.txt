TL2File := TL2Combinator* EOF;
TL2Combinator := TL2Annotation* (TL2TypeDeclaration | TL2FuncDeclaration) scl;

TL2FuncDeclaration := TL2TypeName CRC32 TL2Field* funEq ((alias? TL2TypeRef) | TL2TypeDefinitionStruct);
TL2TypeDeclaration := TL2TypeName CRC32? (lts TL2TypeTemplate (cm TL2TypeTemplate)* gts)? TL2TypeDefinition;

TL2TypeDefinition = (eq TL2StructTypeDefinition) | (alias TL2TypeRef);

TL2StructTypeDefinition := TL2Field* | TL2UnionType;

TL2UnionType := tl2UnionTypeMono | tl2UnionTypeMulti;
tl2UnionTypeMono := vb TL2UnionConstructor (vb TL2UnionConstructor)*;
tl2UnionTypeMulti := TL2UnionConstructor (vb TL2UnionConstructor)+;

TL2UnionConstructor := name (TL2TypeRef | TL2Field*);

TL2Field := ((tl2fieldName qm?) | usc) cl TL2TypeRef;
tl2fieldName := name | depName

TL2TypeRef := TL2TypeApplication | TL2BracketType;

TL2TypeApplication := TL2TypeName (lts TL2TypeArgument (cm TL2TypeArgument)* gts)?;
TL2BracketType := lsb TL2TypeArgument? rsb TL2TypeRef;

TL2TypeArgument := TL2TypeRef | number;
TL2TypeTemplate := name cl TL2Category;

TL2TypeName := (name dot)? name;
TL2Annotation := at lcName;

TL2Category = cNat | cType;

// tokens
CRC32 := #[a-f0-9]{8};
number := [0-9]+;

lcName  := [a-z][a-zA-Z0-9_]*;
ucName  := [A-Z][a-zA-Z0-9_]*;
name    := [a-zA-Z][a-zA-Z0-9_]*;
depName := _name;

alias := <=>;
funEq := =>;
dot := \.;
cm := ,;
eq := =;
cl := :;
scl := \;;
qm := \?;
vb := \|;
lsb := \[;
rsb := \];
lts := <;
gts := >;
at := @;
usc := _;

cNat := #;
cType := Type;